t(){
  if hash tmux 2>/dev/null; then
    if [[ -z "$TMUX" ]] && [[ -z "$SSH_CLIENT" ]] && [[ -z "$SSH_TTY" ]]; then
      tmux
    fi
  fi
}

zsh_internet_signal(){
  if [ -x "$(command -v airport)" ]; then
      local output=$(airport -I)
      local signal=$(echo $output | grep agrCtlRSSI | awk '{print $2}' | sed "s/-//g")
  else
      local signal=0
  fi

  local color="%F{yellow}"
  local symbol="\uf197"

  if [[ "${signal// }" > 0 ]]; then
    local noise=$(echo $output | grep agrCtlNoise | awk '{print $2}' | sed "s/-//g")
    local SNR=$(bc <<< "scale=2; $signal / $noise")

    # Excellent Signal (5 bars)
    if [[ $SNR -gt .40 ]]; then
      color="%F{blue}"; symbol="\uf1eb"
    # Good Signal (3-4 bars)
    elif [[ $SNR -gt .25 ]]; then
      color="%F{green}"; symbol="\uf1eb"
    # Low Signal (2 bars)
    elif [[ $SNR -gt .15 ]]; then
      color="%F{yellow}"; symbol="\uf1eb"
    # Very Low Signal (1 bar)
    elif [[ $SNR -gt .10 ]]; then
      color="%F{red}"; symbol="\uf1eb"
    fi
  else
    local net=$(curl -D- -o /dev/null -s http://www.google.com | grep HTTP/1.1 | awk '{print $2}')

    # Ethernet Connection
    if [[ "$net" -eq 200 ]]; then
      color="%F{blue}"; symbol="\uf0e8"
    # No Internet
    else
      color="%F{red}"; symbol="\uf011"
    fi
  fi

  echo -n "%{$color%}$symbol "
}

if [[ `uname` == "Darwin" ]]; then
  function top() {
    if [ $# -eq 0 ]; then
      /usr/bin/top -o cpu
    else
      /usr/bin/top "$@"
    fi
  }
fi

function nicediff {
  diff -W `tput cols` -y "$@" | colordiff | less
}

function set-title-precmd() {
  printf "\e]2;%.30s\a" "${PWD/#$HOME/~}"
}

function set-title-preexec() {
  printf "\e]2;%.30s\a" "$1"
}

function workon_cwd {
  if [[ -z "$WORKON_CWD" ]]; then
    local WORKON_CWD=1
    # Get absolute path, resolving symlinks
    local PROJECT_ROOT="${PWD:A}"
    while [[ "$PROJECT_ROOT" != "/" && ! -e "$PROJECT_ROOT/.venv" \
        && ! -d "$PROJECT_ROOT/.git" ]]; do
      PROJECT_ROOT="${PROJECT_ROOT:h}"
    done

    # Check for virtualenv name override
    if [[ -f "$PROJECT_ROOT/.venv/bin/activate" ]];then
      ENV_NAME="$PROJECT_ROOT/.venv"
    else
      ENV_NAME=""
    fi

    if [[ -n $CD_VIRTUAL_ENV && "$ENV_NAME" != "$CD_VIRTUAL_ENV" ]]; then
      # We've just left the repo, deactivate the environment
      # Note: this only happens if the virtualenv was activated automatically
      deactivate && unset CD_VIRTUAL_ENV
    fi
    if [[ "$ENV_NAME" != "" ]]; then
      # Activate the environment only if it is not already active
      if [[ -e "$ENV_NAME/bin/activate" ]]; then
        source $ENV_NAME/bin/activate && export CD_VIRTUAL_ENV="$ENV_NAME"
      else
        ENV_NAME=""
      fi
    fi
    if [[ "$ENV_NAME" == "" && -n $CD_VIRTUAL_ENV && -n $VIRTUAL_ENV ]]; then
      # We've just left the repo, deactivate the environment
      # Note: this only happens if the virtualenv was activated automatically
      deactivate && unset CD_VIRTUAL_ENV
    fi
  fi
}
